//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(Events1ID,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
//doors are locked
Door(1,5)
Door(11,3)
END_MAIN

MiscBasedEvents:
DefeatAll(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

OpeningCutscene1:
UNIT Nathan 0x0 0x1 Level(1,Ally,0) [7,9] 0x0 0x0 0x1 REDA7R7 [Wand,Vulnerary,FlameScroll] NoAI
UNIT
OpeningCutscene2:
UNIT Astarte 0x0 0x1 Level(1,NPC,0) [10,0] 0x0 0x0 0x1 REDA10R2 [0x0] NoAI
UNIT
OpeningCutscene3:
UNIT Gore 0x0 0x1 Level(1,NPC,0) [4,0] 0x0 0x0 0x1 REDA4R2 [0x0] NoAI
UNIT
OpeningCutscene4:
UNIT Cleone 0x0 0x1 Level(1,NPC,0) [5,9] 0x0 0x0 0x1 REDA5R8 [0x0] NoAI
UNIT
OpeningCutscene5:
UNIT Rat 0x0 0x1 Level(1,NPC,0) [9,9] 0x0 0x0 0x1 REDA9R8 [0x0] NoAI
UNIT

CutsceneParty:
UNIT Nathan 0x0 0x1 Level(1,Ally,0) [8,11] 0x0 0x0 0x0 0x0 [Wand,Vulnerary,FlameScroll] NoAI
UNIT Astarte 0x0 0x1 Level(2,Ally,0) [9,10] 0x0 0x0 0x0 0x0 [Shortsword,Wand,Vulnerary] NoAI
UNIT Gore 0x0 0x1 Level(2,Ally,0) [7,10] 0x0 0x0 0x0 0x0 [Handgonne,Medica] NoAI
UNIT BossGuildmaster Highlander 0x1 Level(26,NPC,0) [12,0] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Gerson Lancer 0x1 Level(1,NPC,0) [7,3] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT CoastKid 0x0 0x1 Level(1,NPC,0) [2,9] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Brie 0x0 0x1 Level(1,NPC,0) [5,13] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Francesca 0x0 0x1 Level(1,NPC,0) [7,5] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT DreadJenny 0x0 0x1 Level(1,NPC,0) [6,6] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Philmore 0x0 0x1 Level(1,NPC,0) [12,12] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Cassandra 0x0 0x1 Level(1,NPC,0) [13,11] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Trish 0x0 0x1 Level(1,NPC,0) [10,6] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Wendy 0x0 0x1 Level(1,NPC,0) [12,7] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

Units:
UNIT Nathan 0x0 0x1 Level(1,Ally,0) [0,4] 0x0 0x0 0x1 REDA3R8 [Wand,Vulnerary,FlameScroll] NoAI
UNIT Astarte 0x0 0x1 Level(2,Ally,0) [0,4] 0x0 0x0 0x1 REDA2R7 [Shortsword,Wand,Vulnerary] NoAI
UNIT Gore 0x0 0x1 Level(2,Ally,0) [0,3] 0x0 0x0 0x1 REDA2R9 [Handgonne,Medica] NoAI
UNIT

NPCGuildmaster1:
UNIT BossGuildmaster Highlander 0x0 Level(26,NPC,0) [0,3] 0x0 0x0 0x1 REDA0R5 [Fengyan,OutlandShield] NeverMoveAI
UNIT

NPCGuildmaster2:
//put gerson here once he exists
//imperial testing - fucking gorgon egg hardcoding
UNIT BossGuildmaster Highlander 0x0 Level(26,NPC,0) [0,0] 0x0 0x0 0x1 REDA4R0 [Fengyan,OutlandShield] NeverMoveAI
UNIT Gerson Lancer 0x0 Level(1,NPC,0) [0,0] 0x0 0x0 0x1 REDA3R0 [Shortspear,RepairScroll] NeverMoveAI
UNIT AsketaGeneric Driver 0x0 Level(15,NPC,1) [0,0] 0x0 0x0 0x1 REDA2R0 [Dominance] NeverMoveAI
UNIT

EnemyUnits:
UNIT BossCleone DarkHunter 0x0 Level(1,Enemy,0) [7,0] 0x0 0x0 0x1 REDA8R7 [VineWhip] AttackInRangeAI
//lmao he can't be level 12 i don't need fucked up boss abusers
UNIT BossBenthus Noble 0x0 Level(3,Enemy,0) [8,0] DropItem 0x0 0x1 REDA12R7 [Epee,AriadneShard] AttackInRangeAI
UNIT GuildGeneric Swordfighter 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA6R2 [Shortsword] NoAI
UNIT GuildGeneric Axefighter 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA4R5 [Shortmace] NoAI
UNIT GuildGeneric Archer 0x0 Level(1,Enemy,1) [8,0] 0x0 0x0 0x1 REDA14R5 [Shortbow] AttackInRangeAI
UNIT GuildGeneric Brawler 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA8R0 [BrassKnuckles] AttackInRangeAI
UNIT

EnemyUnitsExpert:
UNIT BossCleone DarkHunter 0x0 Level(1,Enemy,0) [7,0] 0x0 0x0 0x1 REDA8R7 [ThornWhip] AttackInRangeAI
UNIT BossBenthus Noble 0x0 Level(3,Enemy,0) [8,0] DropItem 0x0 0x1 REDA12R7 [Epee,AriadneShard] AttackInRangeAI
UNIT GuildGeneric Swordfighter 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA6R2 [Scramasax] NoAI
UNIT GuildGeneric Axefighter 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA4R5 [Shortmace] NoAI
UNIT GuildGeneric Gunner 0x0 Level(1,Enemy,1) [8,0] 0x0 0x0 0x1 REDA14R5 [Handgonne] AttackInRangeAI
UNIT GuildGeneric Brawler 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA8R0 [BrassKnuckles] AttackInRangeAI
UNIT

EnemyUnitsHeroic:
UNIT BossCleone DarkHunter 0x0 Level(1,Enemy,0) [7,0] 0x0 0x0 0x1 REDA8R7 [ThornWhip] AttackInRangeAI
UNIT BossBenthus Noble 0x0 Level(3,Enemy,0) [8,0] DropItem 0x0 0x1 REDA12R7 [Sabre,AriadneShard] AttackInRangeAI
UNIT GuildGeneric Myrmidon 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA6R2 [Scramasax] NoAI
UNIT GuildGeneric Axefighter 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA4R5 [HandAxe] NoAI
UNIT GuildGeneric Gunner 0x0 Level(1,Enemy,1) [8,0] 0x0 0x0 0x1 REDA14R5 [Handgonne] AttackInRangeAI
UNIT GuildGeneric Duelist 0x0 Level(1,Enemy,1) [7,0] 0x0 0x0 0x1 REDA8R0 [Sabre] AttackInRangeAI
UNIT

EndingUnits:
UNIT Nathan 0x0 0x1 Level(1,Ally,0) [6,3] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Astarte 0x0 0x1 Level(2,Ally,0) [7,3] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Gore 0x0 0x1 Level(2,Ally,0) [6,4] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Cleone 0x0 0x1 Level(1,NPC,0) [7,5] 0x0 0x0 0x0 0x0 [VineWhip,Medica] NoAI
UNIT Benthus 0x0 0x1 Level(12,NPC,0) [9,4] 0x0 0x0 0x0 0x0 [Epee,Sabre,Vulnerary] NoAI
UNIT BossGuildmaster Highlander 0x0 Level(26,NPC,0) [8,1] 0x0 0x0 0x0 0x0 [Fengyan,OutlandShield] NoAI
UNIT

ALIGN 4

BeginningScene:
EVBIT_MODIFY 4
EVBIT_F 2
CALL CampaignCustomizers
FADI 16
CLEAN
EVBIT_MODIFY 0
CALL PremonitionScene
//opening text, walk to GM
MUSC Song_OohWilfre
CleanTextNoMap(0x0,C1_Opening_1)
MUSC 0
LOMA Guildhall
LOAD1 0x1 CutsceneParty; ENUN
FADU 10
STAL 40
EVBIT_T 9
MOVE 5 Nathan [10,11]; ENUN
MOVE 5 Nathan [10,9]; ENUN
MOVE 5 Nathan [13,9]; ENUN
MOVE 5 Nathan [13,0]; ENUN
//talk to GM, walk to map positions
CleanTextNoMap(0x1,C1_Opening_2)
CLEA 0; CLEN 0; CLEE 0
LOMA Chapter1
FADU 10
LOAD1 0x1 NPCGuildmaster1; ENUN
STAL 15
SOUN 0xb1
TILECHANGE 0x1
STAL 25
LOAD1 0x1 Units; ENUN
TILEREVERSE 0x1
MOVE 10 BossGuildmaster [0,3]; ENUN
SilentKillUnit(BossGuildmaster)
STAL 30
LOAD1 0x1 NPCGuildmaster2; ENUN
//intro text
Text(C1_Opening_3)
LoadUnitsByDifficulty(EnemyUnits,EnemyUnitsExpert,EnemyUnitsHeroic)
FlashCursor(BossCleone,60)
FlashCursor(BossBenthus,60)
Text(C1_Opening_4)
FlashCursor(Nathan,60)
MUSC Song_TheFirstCampaign
Text(C1_Opening_5)
EndEvent

EndingScene:
//benthus and cleone join, provisional guild
//promise of naming your guild eventually
//music here later
MUSC 0x0
FADI 10
CLEA 0; CLEN 0; CLEE 0
LOAD1 0x1 EndingUnits; ENUN
FADU 10
STAL 30
FlashCursor(Cleone,30)
TurnAlly(Cleone)
FlashCursor(Benthus,30)
TurnAlly(Benthus); STAL 30
CleanTextNoMap(0x0,C1_Ending_1)
MoveToChapter(Chapter2)
ENDA

//todo - free class choosing, 1RN, picnic mode
CampaignCustomizers:
SetBackground(0x35)
SystemText(CampaignCustomPrompt)
IfNoSelectedGoto(0x3000)
SystemText(RTextExplanation)
LABEL 0x1000
RefreshLordSplitTiles
ENUF FIXED_GROWTHS_FLAG_ID; ENUF ZERO_GROWTHS_FLAG_ID; ENUF MIN_GROWTHS_FLAG_ID; ENUF MAX_GROWTHS_FLAG_ID
CallMenu(GrowthModeMenu)
//normal
	SVAL 0x1 0x0
	BNE 0x1 0xc 0x1
		SystemText(NormalGrowthsConfirm)
		IfNoSelectedGoto(0x1000)
		GOTO 0x2000
	LABEL 0x1
//fixed
	SVAL 0x1 0x1
	BNE 0x2 0xc 0x1
		SystemText(FixedGrowthsConfirm)
		IfNoSelectedGoto(0x1000)
		ENUT FIXED_GROWTHS_FLAG_ID
		GOTO 0x2000
	LABEL 0x2
//zero
	SVAL 0x1 0x2
	BNE 0x3 0xc 0x1
		SystemText(ZeroGrowthsConfirm)
		IfNoSelectedGoto(0x1000)
		ENUT ZERO_GROWTHS_FLAG_ID
		GOTO 0x2000
	LABEL 0x3
//min
	SVAL 0x1 0x3
	BNE 0x4 0xc 0x1
		SystemText(MinimumGrowthsConfirm)
		IfNoSelectedGoto(0x1000)
		ENUT MIN_GROWTHS_FLAG_ID
		GOTO 0x2000
	LABEL 0x4
//max
	SVAL 0x1 0x4
	BNE 0x5 0xc 0x1
		SystemText(MaxGrowthsConfirm)
		IfNoSelectedGoto(0x1000)
		ENUT MAX_GROWTHS_FLAG_ID
		GOTO 0x2000
	LABEL 0x5
LABEL 0x2000
ENUT 0x8
LABEL 0x2001
CallMenu(CustomizersMenu)
	SVAL 0x1 0x0
	BNE 0x10 0xc 0x1
		SystemText(PicnicModeConfirm)
		IfNoSelectedGoto(0x2001)
		ENUT PicnicModeFlag
		ENUF 0x8
		GOTO 0x2001
	LABEL 0x10
	SVAL 0x1 0x1
	BNE 0x11 0xc 0x1
		SystemText(ClearModifiers)
		ENUF PicnicModeFlag
		GOTO 0x2000
	LABEL 0x11
LABEL 0x3000
ENUF 0x8
ENDA

PremonitionScene:
TILECHANGE 0x0
FADU 4
LOAD1 0x1 OpeningCutscene1; ENUN; STAL 30
FlashCursor(Nathan,30); SoloText(C1_Premonition_Silence); STAL 30
LOAD1 0x1 OpeningCutscene2; ENUN; STAL 30
FlashCursor(Astarte,30); SoloText(C1_Premonition_Silence); STAL 30
FlashCursor(Nathan,30); SoloText(C1_Premonition_Silence); STAL 30
LOAD1 0x1 OpeningCutscene3; ENUN; STAL 30
FlashCursor(Gore,30); SoloText(C1_Premonition_Silence); STAL 30
FlashCursor(Nathan,30); SoloText(C1_Premonition_Silence); STAL 30
FlashCursor(Astarte,30); SoloText(C1_Premonition_Astarte1)
FlashCursor(Gore,30); SoloText(C1_Premonition_Gore1)
TurnEnemy(Astarte); STAL 30
FlashCursor(Astarte,30); SoloText(C1_Premonition_Astarte2)
TurnEnemy(Gore);STAL 30
FlashCursor(Gore,30); SoloText(C1_Premonition_Gore2); STAL 30
//FlashCursor(Nathan,30); SoloText(C1_Premonition_Silence); STAL 30
LOAD1 0x1 OpeningCutscene4; ENUN; STAL 30
FlashCursor(Cleone,30); SoloText(C1_Premonition_Cleone); STAL 30
LOAD1 0x1 OpeningCutscene5; ENUN; STAL 30
FlashCursor(Rat,30); SoloText(C1_Premonition_Rat); STAL 30
TurnEnemy(Cleone); STAL 30
TurnEnemy(Rat); STAL 30
FlashCursor(Astarte,30);FlashCursor(Gore,30)
FlashCursor(Cleone,30);FlashCursor(Rat,30)
SoloText(C1_Premonition_Ending); FADI 10; CLEE 0
FADU 10
FlashCursor(Nathan,30); SoloText(C1_Premonition_Silence); STAL 30
FADI 4
CLEA 0
TILEREVERSE 0x0
STAL 60
ENDA

//MESSAGE Events end at offset CURRENTOFFSET