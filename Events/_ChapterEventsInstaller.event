//use braces here to avoid name conflicts. If you don't know what that means, surround all #includes in this files in braces like done below.
#include "REDAs.event"

	PUSH
	ORG $A39768
	EVBIT_MODIFY 0x1
	SKIPWN
	ENDA
	POP
	
#define SetUnitStatline(unit,mhp,str,mag,skl,spd,luk,def,res) "SetUnitStatus(unit,MaxHP,mhp);SetUnitStatus(unit,CurrentHP,mhp);SetUnitStatus(unit,Strength,str);SetUnitStatus(unit,Magic,mag);SetUnitStatus(unit,Skill,skl);SetUnitStatus(unit,Speed,spd);SetUnitStatus(unit,LuckStat,luk);SetUnitStatus(unit,Defence,def);SetUnitStatus(unit,Resistance,res)"
#define SetUnitWEXP(unit,swd,lnc,axe,bow,stf) "SetUnitStatus(unit,SwordExp,swd);SetUnitStatus(unit,LanceExp,lnc);SetUnitStatus(unit,AxeExp,axe);SetUnitStatus(unit,BowExp,bow);SetUnitStatus(unit,StaffExp,stf)"

	
#define AreaEvent(eventID,EventPointer,corner1x,corner1y,corner2x,corner2y)   "AREA eventID EventPointer [corner1x,corner1y] [corner2x,corner2y]" 
#define AreaEvent(flag,label,x,y) "AreaEvent(flag,label,x,y,x,y)"
	
#define LoadUnitsByDifficulty(easy,normal,hard) "CHECK_TUTORIAL ; BNE 4034 rC r0 ; CHECK_HARD ; BNE 4035 rC r0 ; LOAD1 0x1 normal ; GOTO 4036 ; LABEL 4034 ; LOAD1 0x1 easy ; GOTO 4036 ; LABEL 4035 ; LOAD1 0x1 hard ; LABEL 4036 ; ENUN;"
#define SystemText(textid) "CenterTutorialTextBox; TUTORIALTEXTBOXSTART; TEXTSHOW textid; TEXTEND; REMA"
#define SoloText(textid) "CenterTutorialTextBox; SOLOTEXTBOXSTART; TEXTSHOW textid; TEXTEND; REMA"
#define SystemText(bgid,textid) "CenterTutorialTextBox; SetBackground(bgid); TUTORIALTEXTBOXSTART; TEXTSHOW textid; TEXTEND; FADI 16"
#define EndEvent "EVBIT_T 0x7; ENDA"
#define CleanTextNoMap(bgid,textid) "SetBackground(bgid); TEXTSTART; TEXTSHOW textid; TEXTEND; FADI 16"
#define CleanText(bgid,textid) "SetBackground(bgid); TEXTSTART; TEXTSHOW textid; TEXTEND; ClearBackgroundSmooth"
#define CGText(textid) "CGTEXTSTART; TEXTSHOW textid; TEXTEND"
#define GiveItem(item) "SMOV 0x3 item; GIVEITEMTO 0xFFFF;"
#define GiveItem(item,character) "SMOV 0x3 item; GIVEITEMTO character;"
#define CamOn "EVBIT_T 9"
#define CamOff "EVBIT_F 9"
#define StartGameOver "ASMC 0x800D391" // Game over ASMC 
#define RefreshSMS "ASMC 0x8026689" //see how this looks when loading stuff
#define RefreshLordSplitTiles "ASMC 0x8003D21" //see how this looks when loading stuff
#define Reinforcements(pointer) "CamOn ; LOAD1 0x0 pointer; ENUN; RefreshSMS; CamOff"
#define HardReinforcements(pointer) "CHECK_HARD ; BNE 6789 rC r0 ; Reinforcements(pointer); LABEL 6789"
#define DifficultyReinforcements(easy,normal,hard) "CHECK_TUTORIAL ; BNE 4034 rC r0 ; CHECK_HARD ; BNE 4035 rC r0 ; Reinforcements(normal) ; GOTO 4036 ; LABEL 4034 ; Reinforcements(easy) ; GOTO 4036 ; LABEL 4035 ; Reinforcements(hard) ; LABEL 4036 ; ENUN;"
#define RemoveUnit(unit) "CHECK_EXISTS unit; BEQ unit 0xc 0x0; REMU unit; LABEL unit"
#define UnremoveUnit(unit) "CHECK_EXISTS unit; BEQ unit 0xc 0x0; REVEAL unit; LABEL unit"
#define TurnAllyIfAlive(unit) "CHECK_ALIVE unit; BEQ unit 0xc 0x0; TurnAlly(unit); LABEL unit"
#define SilentKillUnit(unit) "DISA_IF unit; DISA unit"
#define SFXKillUnit(unit) "SET_SOMETHING unit; DISA unit"
#define DisaIfDead(unit) "CHECK_ALIVE unit; BNE unit 0xc 0x0; DISA unit; LABEL unit"
#define CallPreps "CALL $591FD8"
	
#include "_BattleQuotes.event"
#include "_DeathQuotes.event"
#include "_BossMusic.event"
#include "_ForceDeployer.event"
#include "WorldMapEvents.event"
#include "LordSplitMenus/LordSplitMenuPlus.event"

//GAME OVER STUFF
PUSH
ORG $837B8
BYTE 0x0
ORG $837CA
SHORT 0x0
ORG 0x592104
CALL NewGameOverEvent
ENDA
POP

NewGameOverUnit:
UNIT Nathan 0x0 0x1 Level(1,Ally,0) [7,7] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

NewGameOverEvent:
EVBIT_MODIFY 4
FADI 10
CLEA 0; CLEN 0; CLEE 0
LOMA Chapter1
TILECHANGE 0x0
LOAD1 0x1 NewGameOverUnit; ENUN
SVAL 0x1 99
SET_HP Nathan
FADU 5
STAL 45
SoloText(GameOverText); STAL 30
SFXKillUnit(Nathan); STAL 30
ASMC $8085375
ENDA

MoveResetter:
SVAL 0x1 0x42
UNSET_CONDITION 0xffff
SET_ACTIVE 0
EndEvent

{
#include "Chapters/Chapter1.event"
}
{
#include "Chapters/Chapter2.event"
}
{
#include "Chapters/Chapter3.event"
}
//special city recruit possibilities
//sfl swordmaster
//bertram?
{
#include "City/HirschringSquare.event"
}

ALIGN 4
BlankEscape:
EscapePointEnd